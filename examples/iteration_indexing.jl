using BinaryTraits
using BinaryTraits.Prefix: Is, Not

# See https://docs.julialang.org/en/v1/manual/interfaces

# -----------------------------------------------------------------------------
# Iteration interface
# -----------------------------------------------------------------------------
import Base: iterate
@trait Iterable
@implement Is{Iterable} by iterate(_)::Any
@implement Is{Iterable} by iterate(_, state::Any)::Any

# Example from https://docs.julialang.org/en/v1/manual/interfaces/#man-interface-iteration-1
struct Squares
    count::Int
end

Base.iterate(S::Squares, state=1) = state > S.count ? nothing : (state*state, state+1)

# Let's assign the Squares type to Iterable
@assign Squares with Is{Iterable}
@check(Squares)
#=
julia> @check(Squares)
âœ… Squares has implemented:
1. BinaryTrait{Iterable}: Positive{Iterable} â‡¢ iterate(ðŸ”¹)::Any
2. BinaryTrait{Iterable}: Positive{Iterable} â‡¢ iterate(ðŸ”¹, ::Any)::Any
=#

# -----------------------------------------------------------------------------
# Indexing interface
# -----------------------------------------------------------------------------
import Base: getindex, setindex!, firstindex, lastindex

@trait Indexable
@implement Is{Indexable} by getindex(_, i::Any)

@trait IndexableFromBeginning
@implement Is{IndexableFromBeginning} by firstindex(_)

@trait IndexableAtTheEnd
@implement Is{IndexableAtTheEnd} by lastindex(_)

# Make sure that `i` is untyped (i.e. `Any`) to adhere to the contract
function Base.getindex(S::Squares, i)
    1 <= i <= S.count || throw(BoundsError(S, i))
    return i*i
end

@assign Squares with Is{Indexable}
@check(Squares)
#=
julia> @check(Squares)
âœ… Squares has implemented:
1. BinaryTrait{Iterable}: Positive{Iterable} â‡¢ iterate(ðŸ”¹)::Any
2. BinaryTrait{Iterable}: Positive{Iterable} â‡¢ iterate(ðŸ”¹, ::Any)::Any
3. BinaryTrait{Indexable}: Positive{Indexable} â‡¢ getindex(ðŸ”¹, ::Any)::Any
=#

# We want to have the traits for indexing from beginning and at the end
@assign Squares with Is{IndexableFromBeginning}, Is{IndexableAtTheEnd}
@check(Squares)
#=
julia> @check(Squares)
â”Œ Warning: Missing implementation
â”‚   contract = BinaryTrait{IndexableFromBeginning}: Positive{IndexableFromBeginning} â‡¢ firstindex(ðŸ”¹)::Any
â”” @ BinaryTraits ~/.julia/dev/BinaryTraits.jl/src/interface.jl:59
â”Œ Warning: Missing implementation
â”‚   contract = BinaryTrait{IndexableAtTheEnd}: Positive{IndexableAtTheEnd} â‡¢ lastindex(ðŸ”¹)::Any
â”” @ BinaryTraits ~/.julia/dev/BinaryTraits.jl/src/interface.jl:59
âœ… Squares has implemented:
1. BinaryTrait{Iterable}: Positive{Iterable} â‡¢ iterate(ðŸ”¹)::Any
2. BinaryTrait{Iterable}: Positive{Iterable} â‡¢ iterate(ðŸ”¹, ::Any)::Any
3. BinaryTrait{Indexable}: Positive{Indexable} â‡¢ getindex(ðŸ”¹, ::Any)::Any
âŒ Squares is missing these implementations:
1. BinaryTrait{IndexableFromBeginning}: Positive{IndexableFromBeginning} â‡¢ firstindex(ðŸ”¹)::Any
2. BinaryTrait{IndexableAtTheEnd}: Positive{IndexableAtTheEnd} â‡¢ lastindex(ðŸ”¹)::Any
=#

# Let's implement them now.
Base.firstindex(S::Squares) = 1
Base.lastindex(S::Squares) = length(S)
@check(Squares)
#=
julia> @check(Squares)
âœ… Squares has implemented:
1. BinaryTrait{IndexableFromBeginning}: Positive{IndexableFromBeginning} â‡¢ firstindex(ðŸ”¹)::Any
2. BinaryTrait{Iterable}: Positive{Iterable} â‡¢ iterate(ðŸ”¹)::Any
3. BinaryTrait{Iterable}: Positive{Iterable} â‡¢ iterate(ðŸ”¹, ::Any)::Any
4. BinaryTrait{Indexable}: Positive{Indexable} â‡¢ getindex(ðŸ”¹, ::Any)::Any
5. BinaryTrait{IndexableAtTheEnd}: Positive{IndexableAtTheEnd} â‡¢ lastindex(ðŸ”¹)::Any
=#
